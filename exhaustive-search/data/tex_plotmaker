set terminal epslatex color size 5.2,4
set out 'tve_gnu.tex'
set xlabel "$\\log_{10}(\\epsilon^{-1})$"
set ylabel "$N_{\\mathcal{R}}$"

set style fill transparent solid 0.25

set xrange [0:10]
set yrange [0:40]
set key spacing 1.2
#set ytics 0.1
#set key at graph 0.45,0.15
set key at graph 1.0,0.35

#f(x,ab,p)=(log(ab)+(p**2-1)*log(1/x))/log(p*(p-1))
f(x,ab,p)=(log(ab)+(p**2-1)*x*log(10))/log(p*(p-1))

pi=3.14159265359
#set datafile separator ","
g(x)=a+b*x
fit g(x) 'sde_results' u (log(1/$4)/log(10)):2 via a,b
plot f(x,1.0/16/pi,2) w lines lw 5 dt 3 lc rgb "#E69F00" ti '$p=2$',\
	f(x,10.0/9/sqrt(3.0)/pi**3,3) w lines lw 5 dt 3 lc rgb "#009E73" ti '$p=3$',\
	x*log(10)/log(2) w lines dt 4 lw 5 lc rgb "#E69F00" ti '$\log_{2}(\epsilon^{-1})$',\
	x*log(10)/log(3) w lines dt 4 lw 5 lc rgb "#009E73" ti '$\log_{3}(\epsilon^{-1})$',\
	1.15*x*log(10)/log(2)+9.2 w lines lw 5 lt 5 lc rgb "#56B4E9" ti 'RUS',\
	g(x) w lines lw 5 lt 5 lc rgb "black" notitle,\
	'sde_results' u (log(1/$4)/log(10)):2 w points pt 5 lc rgb "black" ti 'This work' 
